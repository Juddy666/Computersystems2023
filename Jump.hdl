// File name: Jump.hdl

/**
 * The jump unit determines whether the Program Counter should jump.
 * A jump is performed if any of the j1 j2 j3 bits of the c-instruction 
 * are true, AND the matching condition is met:
 *   j1: jump if ALU output is < 0
 *   j2: jump if ALU output   == 0
 *   j3: jump if ALU output is > 0
 * If multiple of these bits are set true, then jump if any of 
 * those conditions are met,
 * e.g. if j1 and j2 are both set true, jump if < 0 or == 0 
 * The ALU output is determined through the zr and ng wires:
 *   zr: is true when the ALU's output   == 0, false otherwise
 *   ng: is true when the ALU's output is < 0, false otherwise
 * If both zr and ng are false, then the ALU's output is > 0
 */

CHIP Jump {

    IN  j1,             // c-instruction j1
        j2,             // c-instruction j2
        j3,             // c-instruction j3
        zr,             // zr pin from ALU
        ng;             // ng pin from ALU

    OUT jump;           // true if a jump should be performed, false otherwise 

PARTS:
    //j1 and j2 conditions
    And(a=j1, b=ng, out=jumpj1);
    And(a=j2, b=zr, out=jumpj2);
    
    //j3 condition
    Not(in=zr, out=notzr);
    Not(in=ng,out=ps);
    And(a=notzr, b=ps, out=positive);
    And(a=j3, b=positive,out=jumpj3);

    //combine exclusive conditions
    Or(a=jumpj1, b=jumpj2, out=jumpj1OR2);
    Or(a=jumpj1OR2, b=jumpj3, out=jumpex);


    //All j conditions
    And(a=j1, b=j2, out=j1ANDj2);
    And(a=j1ANDj2, b=j3, out=jumpall);

    //combine final conditions
    Or(a=jumpex, b=jumpall, out=jump);

    
}